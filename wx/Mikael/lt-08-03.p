/* lt-08-01.p */

/******************* DEFINE QUERY ***********************/
DEFINE QUERY persq FOR PERSONALTAB.

/***************** DEFINE VARIABLES *********************/
DEFINE VARIABLE cur-rec AS ROWID.
DEFINE VARIABLE answer AS LOGICAL.

/****************** DEFINE WIDGETS **********************/
DEFINE BUTTON BTN-PREV LABEL "Prev".
DEFINE BUTTON BTN-NEXT LABEL "Next".
DEFINE BUTTON BTN-UPDATE LABEL "Update".
DEFINE BUTTON BTN-ADD LABEL "Add".
DEFINE BUTTON BTN-DELETE LABEL "Delete".
DEFINE BUTTON BTN-EXIT LABEL "Exit".

DEFINE BUTTON BTN-OK LABEL "Ok" AUTO-GO.
DEFINE BUTTON BTN-CANCEL LABEL "Cancel" AUTO-ENDKEY.

/******************* DEFINE FRAMES **********************/
DEFINE FRAME Frame1
   SKIP(1)
   PERSONALTAB.PERSONALKOD LABEL "Personalkod" COLON 15
   PERSONALTAB.EFTERNAMN LABEL "Efternamn" COLON 15
   PERSONALTAB.FORNAMN LABEL "Förnamn" COLON 15
   PERSONALTAB.GATUADRESS LABEL "Gatuadress" COLON 15
   PERSONALTAB.POSTNUMMER LABEL "Postnummer" COLON 15
   PERSONALTAB.POSTADRESS LABEL "Postadress" COLON 15 
   SKIP(1)
   BTN-PREV TO 7
   BTN-NEXT 
   BTN-UPDATE
   BTN-ADD
   BTN-DELETE
   BTN-EXIT 
   SKIP(1) 
   WITH SIDE-LABELS CENTERED ROW 2 THREE-D
   TITLE "Database Access Form for the Personaltab Table".

DEFINE FRAME Dialog1
   SKIP(1)
   PERSONALTAB.PERSONALKOD LABEL "Personalkod" COLON 15
   PERSONALTAB.EFTERNAMN LABEL "Efternamn" COLON 15
   PERSONALTAB.FORNAMN LABEL "Förnamn" COLON 15
   PERSONALTAB.GATUADRESS LABEL "Gatuadress" COLON 15
   PERSONALTAB.POSTNUMMER LABEL "Postnummer" COLON 15
   PERSONALTAB.POSTADRESS LABEL "Postadress" COLON 15 
   SKIP(1)
   BTN-OK TO 15
   BTN-CANCEL
   SKIP
   WITH VIEW-AS DIALOG-BOX THREE-D SIDE-LABELS TITLE "Update Personaltab Record".

/****************** DEFINE TRIGGERS *********************/
ON CHOOSE OF BTN-PREV
DO:
   GET PREV persq.
   IF QUERY-OFF-END("persq") THEN GET LAST persq.
   DISPLAY PERSONALTAB.PERSONALKOD PERSONALTAB.EFTERNAMN PERSONALTAB.FORNAMN 
      PERSONALTAB.GATUADRESS PERSONALTAB.POSTNUMMER PERSONALTAB.POSTADRESS 
      WITH FRAME Frame1.
END.

ON CHOOSE OF BTN-NEXT
DO:
   GET NEXT persq.
   IF QUERY-OFF-END("persq") THEN GET FIRST persq.
   DISPLAY PERSONALTAB.PERSONALKOD PERSONALTAB.EFTERNAMN PERSONALTAB.FORNAMN 
      PERSONALTAB.GATUADRESS PERSONALTAB.POSTNUMMER PERSONALTAB.POSTADRESS 
      WITH FRAME Frame1.
END.

ON CHOOSE OF BTN-UPDATE
DO:
   cur-rec = ROWID(PERSONALTAB).
   FIND FIRST PERSONALTAB WHERE ROWID(PERSONALTAB) = cur-rec
      EXCLUSIVE-LOCK.
   IF AVAILABLE(PERSONALTAB) THEN DO:
      UPDATE PERSONALTAB.EFTERNAMN PERSONALTAB.FORNAMN PERSONALTAB.GATUADRESS 
         PERSONALTAB.POSTNUMMER PERSONALTAB.POSTADRESS BTN-OK BTN-CANCEL
         WITH FRAME Dialog1.
      DISPLAY PERSONALTAB.PERSONALKOD PERSONALTAB.EFTERNAMN PERSONALTAB.FORNAMN 
         PERSONALTAB.GATUADRESS PERSONALTAB.POSTNUMMER PERSONALTAB.POSTADRESS 
         WITH FRAME Frame1.
   END.
   ELSE DO:
      FIND FIRST PERSONALTAB WHERE ROWID(PERSONALTAB) = cur-rec NO-LOCK.
      MESSAGE "Record in use. Unable to update."
         VIEW-AS ALERT-BOX WARNING BUTTONS OK TITLE "Update Error".
   END.
END.

ON CHOOSE OF BTN-ADD
DO:
   CREATE PERSONALTAB.
   UPDATE PERSONALTAB.PERSONALKOD PERSONALTAB.EFTERNAMN PERSONALTAB.FORNAMN PERSONALTAB.GATUADRESS 
         PERSONALTAB.POSTNUMMER PERSONALTAB.POSTADRESS BTN-OK BTN-CANCEL
         WITH FRAME Dialog1.
   DISPLAY PERSONALTAB.PERSONALKOD PERSONALTAB.EFTERNAMN PERSONALTAB.FORNAMN 
      PERSONALTAB.GATUADRESS PERSONALTAB.POSTNUMMER PERSONALTAB.POSTADRESS 
      WITH FRAME Frame1.
END.

ON CHOOSE OF BTN-DELETE
DO:
   MESSAGE "Do you relly want to delete" PERSONALTAB.FORNAMN "?"
      VIEW-AS ALERT-BOX QUESTION BUTTONS YES-NO UPDATE answer.
   IF answer THEN DO:
      cur-rec = ROWID(PERSONALTAB).
      FIND FIRST PERSONALTAB WHERE ROWID(PERSONALTAB) = cur-rec
         EXCLUSIVE-LOCK NO-ERROR.
      IF AVAILABLE(PERSONALTAB) THEN DO:
         DELETE PERSONALTAB.
         OPEN QUERY persq FOR EACH PERSONALTAB NO-LOCK.
         GET FIRST persq.
         DISPLAY PERSONALTAB.PERSONALKOD PERSONALTAB.EFTERNAMN PERSONALTAB.FORNAMN 
            PERSONALTAB.GATUADRESS PERSONALTAB.POSTNUMMER PERSONALTAB.POSTADRESS 
            WITH FRAME Frame1.
      END.
      ELSE DO:
         FIND FIRST PERSONALTAB WHERE ROWID(PERSONALTAB) = cur-rec NO-LOCK.
         MESSAGE "Record in use. Unable to update."
            VIEW-AS ALERT-BOX WARNING BUTTONS OK TITLE "Update Error".
      END.
   END.
END.

/******************** MAIN LOGIC ************************/
OPEN QUERY persq FOR EACH PERSONALTAB NO-LOCK.
GET FIRST persq.
DISPLAY PERSONALTAB.PERSONALKOD PERSONALTAB.EFTERNAMN PERSONALTAB.FORNAMN 
   PERSONALTAB.GATUADRESS PERSONALTAB.POSTNUMMER PERSONALTAB.POSTADRESS 
   WITH FRAME Frame1 USE-TEXT.
ENABLE BTN-PREV BTN-NEXT BTN-UPDATE BTN-ADD BTN-DELETE BTN-EXIT WITH FRAME Frame1.

/******************** WAIT-FOR ************************/
WAIT-FOR CHOOSE OF BTN-EXIT IN FRAME Frame1.
CLOSE QUERY persq.

