/*BYTPRTID.P*/
/*Byt projektnummer for tid som inte har sammanstallts*/
&Scoped-define NEW NEW
{GLOBVAR2DEL1.I}
DEFINE INPUT PARAMETER vaonr AS CHARACTER NO-UNDO.
DEFINE INPUT PARAMETER vdelnr AS INTEGER NO-UNDO.
DEFINE INPUT PARAMETER gaonr AS CHARACTER NO-UNDO.
DEFINE INPUT PARAMETER gdelnr AS INTEGER NO-UNDO.
DEFINE INPUT PARAMETER cglobanv AS CHARACTER NO-UNDO.
     

DEFINE VARIABLE pkod AS CHARACTER NO-UNDO.
{SOKDEF.I}

DEFINE BUFFER aonrbuff FOR AONRTAB.
FIND FIRST FORETAG NO-LOCK NO-ERROR.
ASSIGN Guru.Konstanter:globforetag = FORETAG.FORETAG.
{FORESTYR.I}
FIND AONRTAB WHERE AONRTAB.AONR = vaonr AND AONRTAB.DELNR = vdelnr NO-LOCK NO-ERROR.
FIND aonrbuff WHERE aonrbuff.AONR = gaonr AND aonrbuff.DELNR = gdelnr NO-LOCK NO-ERROR.

OPEN QUERY tq FOR EACH TIDREGITAB WHERE TIDREGITAB.AONR = gaonr AND 
TIDREGITAB.DELNR = gdelnr AND TIDREGITAB.VECKOKORD = "" USE-INDEX PVKORD NO-LOCK.
GET FIRST tq NO-LOCK.
DO WHILE AVAILABLE(TIDREGITAB):
   DO TRANSACTION:           
      IF tidregitab.tidlog = TRUE THEN DO:
         {SOKSTART.I}
         ASSIGN
         soktemp.SOKVAL = 1
         soktemp.SOKINT[1] = Guru.Konstanter:varforetypval[4]
         soktemp.SOKCHAR[2] = TIDREGITAB.PERSONALKOD
         soktemp.SOKCHAR[3] = TIDREGITAB.PRISTYP
         soktemp.SOKCHAR[4] = TIDREGITAB.OVERTIDTILL
         soktemp.SOKDATE[1] = TIDREGITAB.DATUM.
         {SOKANROP.I}
         GET CURRENT tq EXCLUSIVE-LOCK.
         TIDREGITAB.PRIS = soktemp.SOKDECI[1].
         IF TIDREGITAB.PRISTYP = "RESTID..." OR TIDREGITAB.PRISTYP = "FRÅNVARO." THEN TIDREGITAB.PRISTYP = TIDREGITAB.PRISTYP.
         ELSE TIDREGITAB.PRISTYP = AONRTAB.PRISTYP.                 
      END.
      ASSIGN TIDREGITAB.AONR = vaonr 
      TIDREGITAB.DELNR = vdelnr.       
   END.
   GET NEXT tq NO-LOCK.
END.
/* Byt område och beställare om nya projektet har annat*/
OPEN QUERY kalkq FOR EACH KALKAONR WHERE
KALKAONR.AONR = AONRTAB.AONR AND
KALKAONR.DELNR = AONRTAB.DELNR NO-LOCK,
EACH FASTSPEC WHERE FASTSPEC.KALKNR = KALKAONR.KALKNR NO-LOCK.
GET FIRST kalkq NO-LOCK.
DO WHILE AVAILABLE(FASTSPEC):
   DO TRANSACTION:
      GET CURRENT kalkq EXCLUSIVE-LOCK.
      ASSIGN      
      FASTSPEC.OMRADE = AONRTAB.OMRADE
      FASTSPEC.BESTID = AONRTAB.BESTID.
   END.         
   GET NEXT kalkq NO-LOCK.
END.
CLOSE QUERY kalkq.            
OPEN QUERY fkalkq FOR EACH KALKSPEC WHERE 
KALKSPEC.AONR = AONRTAB.AONR AND 
KALKSPEC.DELNR = AONRTAB.DELNR NO-LOCK.            
GET FIRST fkalkq NO-LOCK.
DO WHILE AVAILABLE(KALKSPEC):
   DO TRANSACTION:
      GET CURRENT fkalkq EXCLUSIVE-LOCK.
      ASSIGN      
      KALKSPEC.OMRADE = AONRTAB.OMRADE
      KALKSPEC.BESTID = AONRTAB.BESTID.
   END.         
   GET NEXT fkalkq NO-LOCK.
END.
CLOSE QUERY fkalkq.            
OPEN QUERY kalkaoq FOR EACH KALKAONR WHERE 
KALKAONR.AONR = AONRTAB.AONR AND 
KALKAONR.DELNR = AONRTAB.DELNR NO-LOCK.            
GET FIRST kalkaoq NO-LOCK.
DO WHILE AVAILABLE(KALKAONR):
   DO TRANSACTION:
      GET CURRENT kalkaoq EXCLUSIVE-LOCK.
      ASSIGN      
      KALKAONR.OMRADE = AONRTAB.OMRADE.                  
   END.         
   GET NEXT kalkaoq NO-LOCK.
END.
CLOSE QUERY kalkaoq.

DO TRANSACTION:  
   FIND FIRST VARDERING WHERE VARDERING.AONR = AONRTAB.AONR AND
   VARDERING.DELNR = AONRTAB.DELNR EXCLUSIVE-LOCK NO-ERROR.
   IF AVAILABLE VARDERING THEN DO:
      ASSIGN VARDERING.OMRADE = AONRTAB.OMRADE.       
   END.
END.
DO TRANSACTION:         
   CREATE BYTAONR.      
   ASSIGN
   BYTAONR.AONR = aonrbuff.AONR
   BYTAONR.DELNR = aonrbuff.DELNR
   BYTAONR.OMRADE = aonrbuff.OMRADE
   BYTAONR.BESTID = aonrbuff.BESTID
   BYTAONR.NAONR = vaonr 
   BYTAONR.NDELNR = vdelnr 
   BYTAONR.NOMRADE = AONRTAB.OMRADE 
   BYTAONR.NBESTID = AONRTAB.BESTID 
   BYTAONR.DATUM = TODAY
   BYTAONR.ARTAL = YEAR(TODAY)
   BYTAONR.ANVANDARE = Guru.Konstanter:globanv.      
END.


