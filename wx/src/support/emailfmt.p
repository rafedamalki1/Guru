/******************************************************************************
**** See "NOTE TO SITE" for suggestions and caveats in the attached .p file ***
**** which follows the text below. These "NOTEs" punctuate the program at   ***
**** appropriate places.  Please read these carefully before customizing.   ***
********************************************************************* 1194a ***

         ---- FROM PROGRESS TECHNICAL SUPPORT  ----- 

      PROGRESS TURNS ON NEW CUSTOMER SERVICE PHONE SYSTEM AND 
               EMAIL-INITIATED TECH SUPPORT ISSUES 

Dear Customers:

You've told us that you believe reducing the time to reach
a Technical Support Engineer is one of the most important
improvements that PROGRESS Technical Support can make for you.

We're listening!  In order to serve you better, Progress
Software Corporation's Technical Support has made two
important changes this summer.  We have implemented

    - a new customer service phone system, and 
    - initiated use of electronic mail to receive customer issues. 

NEW PHONE SERVICE
=================
You will find a new voice when you reach
Progress Technical Support at 617-280-4999,  
allowing you to pick from among 4 choices.

OPTION 1.  PRODUCTION SYSTEM IS NOT OPERATIONAL
Our new phone service will allow you to alert us if you have a
production system which is not operational.  These customers
will, as they are now, be handled with priority service.

OPTION 2.  TEST DRIVE
Test Drive customers will have the option of receiving assistance
on installation of media, or of having the call serviced by Sales
Support for all other questions.

OPTION 3.  EVENT-DRIVEN/GUI FEATURES PROGRAMMING
Developers needing assistance in the area of event-driven
applications and graphical user interface using new PROGRESS 
Version 7 product features, such as the User Interface
Builder, triggers, widgets, and browers, will have the
option of directing their query to an applications engineer.

OPTION 4.  ALL OTHER SUPPORT ISSUES
For all other questions -- such as error troubleshooting,
tuning performance, installing, deploying the application,
administering the database, using PROGRESS utilities,
updating product releases, understanding core and optional
products such as 4GL, RESULTS, DataServers, or HLI/C,
or reporting bugs -- under any version or platform of
PROGRESS -- your call will be serviced by the next
available Technical Support Engineer. 

We hope that by directing your call more quickly to the right
resource, that we can help get you the information you seek 
as efficiently and as effectively as possible.

In addition, if you should find that an engineer is not
available and your call is placed on hold, you will have the
choice periodically to leave voicemail and receive a return
phone call. 

EMAIL SUPPORT
=============
Also new for Progress Tech Support in this summer is the 
introduction of the use of electronic mail to take in new
customer issues.   A standard format is available from
Technical Support (and is attached to this transmission)
in the form of a PROGRESS procedure.

Use of this procedure will ensure that the information necessary to
handle your issue in the most expedient fashion possible
is included in your transmission to us.  Your email communications
generated by the procedure should be sent to:

     support@bedford.progress.com

Communications in the standard format will get an
automated reply acknowledgement
as soon as they reach us, complete with your own
issue reference number for tracking purposes.  Your 
acknowledgement will appear illustrated below:

****************************************************************************** 
Date: Wed, 29 Jun 1994 15:56:43 -0400
Message-Id: <9406291956.AA03086@tsserv.bedford.progress.COM>
From: support@progress.com (PSC Tech Support)
To: <YOUR EMAIL ADDRESS HERE>
Subject: Re: Your mail
Status: R

Your email to Progress Technical Support sent on 06/29/94 was received. The
call reference number assigned to your issue is 940629-57402. Please use this
call number for any followup with Progress regarding this issue. You will be
contacted by a Technical Support Engineer once your issue has been reviewed.

- PSC Technical Support

******************************************************************************

Contact Technical Support by phone (617/280-4999) or fax
(617/275-4543) to receive your copy of the email format
procedure to set up your email communication (if you are
not receiving this notice already via electronic mail). 
With email, as in the case of all other support methods,
your PROGRESS product must be covered by a maintenance
agreement in order for us to provide you with a consultation.
As is our current practice, customers outside the U.S. and
Canada should direct their inquiries to their local PROGRESS
subsidiary or distributor.

QUICK REFERENCE
===============
Below is a quick reference summary for you to use.  Keep it 
handy whenever you need to consult with Progress Technical Support.  

         +------------------------------------------------+
         |                                                |
         |  PROGRESS TECHNICAL SUPPORT   617/280-4999     |
         |  FAX:   617/275-4543                           |
         |  EMAIL: support@bedford.progress.com           |
         |  HOURS: 08:30 - 20:00 Eastern U.S. Time M-F    |
         |                                                |
         | Help us help you by having this ready          |
         | - Call Reference # for follow up on existing   |
         |   issues                                       |
         | - Your PROGRESS serial number                  |
         | - Your operating system version                |
         | - Your problem summary                         |
         |                                                |
         +------------------------------------------------+

EMAIL FORMAT PROCEDURE
======================

Below is a procedure designed for V6 or V7 character-based
PROGRESS environments.  It leads the user through a series of
data-gathering steps, so that a structured, standard
format output file can be produced when submitting issues to
Technical Support via email.  No database connection is
required.  Using this procedure when 
preparing a communication to Technical Support will help 
ensure we are able to service your issue in the most efficient
and effective way possible.  Use it whenever you have an issue
for Technical Support.

Further information on email support can be found in the
latest copy of the Technical Bulletin.  Additional information
will be available in the _Connections_ and _PROFILES_ 
magazines later this summer.

*/

/**** NOTE TO SITE ***********************************************************/
/**** The following code will run in V6 & V7 Character Environments       ****/
/**** Under V6, the -e parameter must be at least 63.                     ****/
/**** If you need to run this with V7GUI, the frames should display       ****/
/**** properly, but you will need to use <TAB> and <GO> to navigate       ****/
/**** between fields and frames.                                          ****/
/*****************************************************************************/
/* -------------------- (c) 1994 Progress Software ------------------------- */
/* ----                                                                 ---- */
/* ---- Program Name : emailfmt.p                                       ---- */
/* ---- Description  : Prepares structured report for submitting        ---- */
/* ----              : issues to PROGRESS Technical Support             ---- */
/* ----                                                                 ---- */
/* ---- Author       : P. LeClair, E. Prokopow, H. K. Otaguro           ---- */
/* ---- Date Started : May 1994                                         ---- */
/* ---- Called From  :                                                  ---- */
/* ---- Calls        :                                                  ---- */
/* ---- Includes     : emailqst.i   ******merged 6/21/94****            ---- */
/* ---- Comments     :                                                  ---- */
/* ----                                                                 ---- */
/* ---- Modify Date  : June 21, 1994                                    ---- */
/* ---- Mod Author   : Harry K. Otaguro                                 ---- */
/* ---- Description  : Merge emailqst.i                                 ---- */
/* ---- Comments     :                                                  ---- */
/* ---- Modify Date  : October 18, 1994                                 ---- */
/* ---- Mod Author   : Harry K. Otaguro                                 ---- */
/* ---- Description  : Adjust Frames for GUI Display                    ---- */
/* ---- Comments     : Relocate OUTPUT TO FOR HEADERS                   ---- */
/* ----                                                                 ---- */
/* ---- Modify Date  : November 1, 1994                           1194a ---- */
/* ---- Mod Author   : Harry K. Otaguro                                 ---- */
/* ---- Description  : Add FORMAT VALIDATION FOR CALL NUMBER            ---- */
/* ---- Comments     :                                                  ---- */
/* ----                                                                 ---- */
/* ------------------------------------------------------------------------- */
DEFINE VARIABLE bug         AS LOGICAL   FORMAT "Y/N"                  NO-UNDO.
DEFINE VARIABLE callno      AS CHARACTER FORMAT "x(12)"                NO-UNDO.
DEFINE VARIABLE cur-status  AS LOGICAL   FORMAT "OLD/NEW"              NO-UNDO.
DEFINE VARIABLE desc1       AS CHARACTER FORMAT "x(60)" EXTENT 15      NO-UNDO.
DEFINE VARIABLE desc2       AS CHARACTER FORMAT "x(60)" EXTENT 15      NO-UNDO.
DEFINE VARIABLE follow-up   AS LOGICAL   FORMAT "Y/N"                  NO-UNDO.
DEFINE VARIABLE i           AS INTEGER                                 NO-UNDO.
DEFINE VARIABLE machine     AS CHARACTER FORMAT "x(20)"                NO-UNDO.
DEFINE VARIABLE memory      AS CHARACTER                               NO-UNDO.
DEFINE VARIABLE op-sys      AS CHARACTER FORMAT "x(15)"                NO-UNDO.
DEFINE VARIABLE other       AS CHARACTER FORMAT "x(30)"                NO-UNDO.
DEFINE VARIABLE outfile     AS CHARACTER FORMAT "x(30)"                NO-UNDO.
DEFINE VARIABLE overs       AS CHARACTER                               NO-UNDO.
DEFINE VARIABLE prog-prod   AS CHARACTER FORMAT "x(45)"                NO-UNDO.
DEFINE VARIABLE prog-sn     AS CHARACTER FORMAT "x(9)"                 NO-UNDO.
DEFINE VARIABLE prog-vers   AS CHARACTER FORMAT "9x(7)"                NO-UNDO.
DEFINE VARIABLE rec-name    AS CHARACTER FORMAT "x(3)"  INITIAL "PSC"  NO-UNDO.
DEFINE VARIABLE send-email  AS CHARACTER FORMAT "x(32)"                NO-UNDO.
DEFINE VARIABLE sender-name AS CHARACTER FORMAT "x(30)"                NO-UNDO.
DEFINE VARIABLE snd-company AS CHARACTER FORMAT "x(30)"                NO-UNDO.
DEFINE VARIABLE tdate       AS DATE                     INITIAL TODAY  NO-UNDO.
DEFINE VARIABLE urgent      AS CHARACTER FORMAT "x(1)"                 NO-UNDO.
DEFINE VARIABLE users       AS CHARACTER                               NO-UNDO.

/************ VARIABLES USED IN QUESTIONAIRE PORTION OF REPORT ***************/
DEFINE VARIABLE t-add-lb AS CHARACTER FORMAT "X(7)"  EXTENT 70         NO-UNDO.
DEFINE VARIABLE t-addnl  AS CHARACTER FORMAT "X(20)" EXTENT 70         NO-UNDO.
DEFINE VARIABLE t-labels AS CHARACTER FORMAT "X(14)" EXTENT 70         NO-UNDO.
DEFINE VARIABLE t-msg-lb AS CHARACTER FORMAT "X(7)"  EXTENT 70         NO-UNDO.
DEFINE VARIABLE t-messg  AS CHARACTER FORMAT "X(20)" EXTENT 70         NO-UNDO.
DEFINE VARIABLE t-on7    AS CHARACTER FORMAT "X"     EXTENT 70         NO-UNDO.
DEFINE VARIABLE t-topics AS CHARACTER FORMAT "X"     EXTENT 70         NO-UNDO.
DEFINE VARIABLE t-ver-lb AS CHAR FORMAT "X(5)"  EXTENT 70 INIT "Vers." NO-UNDO.
DEFINE VARIABLE t-versn  AS CHARACTER FORMAT "X(10)" EXTENT 70         NO-UNDO.
DEFINE VARIABLE tx7      AS LOGICAL                                    NO-UNDO.
DEFINE VARIABLE txcnt    AS INTEGER                                    NO-UNDO.
DEFINE VARIABLE txrev    AS CHARACTER                                  NO-UNDO.

ASSIGN                        /* This will be appended to first line of      */
txrev = "1194a".              /* Questionaire module in order to track vers  */
                              /* All revisions of Questionaire should carry  */
                              /* unique txrev value.                         */
/****** END QUESTIONAIRE VARIABLES                     ***********************/

FORM
cur-status LABEL "Status"  COLON 8 HELP "Current status of this issue (NEW/OLD)"
urgent     LABEL "Urgency" COLON 30
  HELP "Enter 1-5: 1=DB Down, 2=Follow up, 3=?, 4=Normal queue"
follow-up  LABEL "Follow up" COLON 52 HELP "Is this e-mail a follow up ? Y/N"
bug        LABEL "Bug"     COLON 70   HELP "Does this concern a bug ? Y/N"
prog-prod  LABEL "PROGRESS Products" COLON 20 HELP "PROGRESS products involved"
prog-vers  LABEL "Version" COLON 20 HELP "PROGRESS Vers#, First Char Must be #"
  VALIDATE(INDEX("123456789",SUBSTRING(prog-vers,1,1)) > 0, "1st chr must be #")
prog-sn    LABEL "Serial number" COLON 20 HELP "Serial# of PROGRESS product"
memory     LABEL "Memory"  COLON 20 HELP "Amnt of memory in subject machine"
users      LABEL "# of Users" COLON 50 
           HELP "# of users accessing this machine"
  VALIDATE(INTEGER(users) >= 0,"Only digits are allowed")
op-sys     LABEL "Operating System" COLON 20 HELP "Operating system(s) involved"
overs      LABEL  "OS Ver." COLON 50 HELP "Operating System version"
machine    LABEL "User Machine" COLON 20 HELP "Subject Machine"
other      LABEL "Other Software" COLON 20 HELP "Other software involved "
WITH FRAME eml-dtl 1 DOWN CENTERED SIDE-LABEL
TITLE "------------------------- Detail Summary -------------------------".

/**** NOTE TO SITE ************************************************************/
/*** THE FOLLOWING FRAME emhead AND ALL VARIABLES USED WITHIN IT SHOULD NOT ***/
/*** BE ALTERED IN ANY WAY - PROGRESS'S E-MAIL SUPPORT FILTER EXPECTS TO BE ***/
/*** ABLE TO READ THIS HEADER IN A SPECIFIC FORMAT ****************************/

FORM
SPACE(18) "PROGRESS SOFTWARE TECHNICAL SUPPORT (PSTS)"                      SKIP
SPACE(31) "FOLLOW-UP/RESOLUTION"                                            SKIP
"Date :"    TO 15 tdate "Call Number :" TO 57 callno
  HELP "Leave BLANK for new issues or Enter Call # for open issues"       
  VALIDATE(callno = "" OR
          (LENGTH(callno)                             = 12  AND
           SUBSTRING(callno,1,1)                      = "9" AND
           INDEX("1234567890",SUBSTRING(callno,2,1))  > 0   AND
           INDEX("1234567890",SUBSTRING(callno,3,1))  > 0   AND
           INDEX("1234567890",SUBSTRING(callno,4,1))  > 0   AND
           INDEX("1234567890",SUBSTRING(callno,5,1))  > 0   AND
           INDEX("1234567890",SUBSTRING(callno,6,1))  > 0   AND
           SUBSTRING(callno,7,1)                      = "-" AND
           INDEX("1234567890",SUBSTRING(callno,8,1))  > 0   AND
           INDEX("1234567890",SUBSTRING(callno,9,1))  > 0   AND
           INDEX("1234567890",SUBSTRING(callno,10,1)) > 0   AND
           INDEX("1234567890",SUBSTRING(callno,11,1)) > 0   AND
           INDEX("1234567890",SUBSTRING(callno,12,1)) > 0),
           "Call Number not in proper format")                              SKIP
"To :"      TO 15 rec-name                                                  SKIP
"From :"    TO 15 sender-name
  VALIDATE(sender-name <> "", "May not be left BLANK")                      SKIP
"Company :" TO 15 snd-company
  VALIDATE(snd-company <> "", "May not be left BLANK")                      SKIP
"E-mail :"  TO 15 send-email
  VALIDATE(send-email <> "", "May not be left BLANK")                       SKIP
WITH FRAME emhead NO-BOX NO-LABELS.

FORM
desc1 NO-LABEL
WITH FRAME det1 OVERLAY CENTERED ROW 4
TITLE "--------------------------- DESCRIPTION -------------------------".
FORM
desc2 NO-LABEL
WITH FRAME det2 OVERLAY CENTERED ROW 4
TITLE "------------------------ DESCRIPTION (cont)----------------------".

/**** NOTE TO SITE ************************************************************/
/**** YOU MAY WANT TO CHANGE NAME OF OUTPUT FILE                           ****/
/**** IF YOU WANT TO KEEP IT, ADD SOME PROVISION FOR COPYING IT TO UNIQUE  ****/
/**** NAME, OR ADD PROVISION FOR GENERATING UNIQUE NAME HERE.              ****/

ASSIGN
outfile = "email.txt".

/**** END NOTE TO SITE ********************************************************/

DO TRANSACTION:
    ASSIGN op-sys      = OPSYS
           prog-prod   = progress
           sender-name = USERID("dictdb")
           prog-sn     = STRING(_serial,"999999999").

    /**** NOTE TO SITE ********************************************************/
    /**** SITE DEFINABLE DEFAULTS *********************************************/

    ASSIGN
    /***************************************** override above ************
    sender-name = ""                        /* Name of contact person    */
    **********************************************************************/
    snd-company = ""                        /* Company name              */
    send-email  = "".                       /* E-mail address of contact */

    /**** REMOVE FROM UPDATE STATEMENT IF SET HERE                         ****/

    DISPLAY tdate rec-name sender-name snd-company send-email
    WITH FRAME emhead.

    UPDATE callno
    sender-name snd-company send-email     /* REMOVE IF PRE-FILLED */
    WITH FRAME emhead.

    IF callno <> "" THEN
    ASSIGN cur-status = TRUE
           follow-up  = YES.

    UPDATE cur-status urgent follow-up bug prog-prod prog-vers prog-sn
    memory users op-sys overs machine other WITH FRAME eml-dtl.

    /*=== QUESTIONAIRE SECTION ==============================================*/
    /**** NOTE TO SITE *******************************************************/
    /** DO NOT ALTER THE OUTPUT STYLE OF THIS QUESTIONAIRE SECTION ***********/
    /** PARSING WILL BE RENDERED USELESS IF WORDING/STYLE IS NOT MAINTAINED **/

    ASSIGN                                        /* PROGRESS Tools - General */
    t-labels[1] = "Appl. Compiler"
    t-labels[2] = "Data Dict."
    t-labels[3] = "Interact. SQL"
    t-labels[4] = "4GL"
    t-labels[5] = "Data Admin"
    t-labels[6] = "Proc Library"
    t-labels[7] = ""                                                 /* SPARE */
    t-labels[8] = ""                                                 /* SPARE */
    t-labels[9] = "".                                                /* SPARE */
    ASSIGN                                             /* PROGRESS Tools - V7 */
    t-labels[10] = "Report Builder"
    t-on7[10]    = "Y"
    t-labels[11] = "Debugger"
    t-on7[11]    = "Y"
    t-labels[12] = "Librarian"
    t-on7[12]    = "Y"
    t-labels[13] = "Tran Man"
    t-on7[13]    = "Y"
    t-labels[14] = "Freeze Frame"
    t-on7[14]    = "Y"
    t-labels[15] = "UIB"
    t-on7[15]    = "Y"
    t-labels[16] = ""                                                /* SPARE */
    t-on7[16]    = "Y"
    t-labels[17] = ""                                                /* SPARE */
    t-on7[17]    = "Y"
    t-labels[18] = ""                                                /* SPARE */
    t-on7[18]    = "Y".
    ASSIGN                                               /* Optional Products */
    t-labels[19] = "Embedded SQL"
    t-labels[20] = "HLC"
    t-labels[21] = "RESULTS"
    t-labels[22] = "Toolkit"
    t-labels[23] = ""
    t-labels[24] = ""
    t-labels[25] = "PRGS ODBC Clnt"            /* V7 */   /* Use w/t-addnl &  */
    t-add-lb[25] = "Vendor"                               /* t-versn together */
    t-on7[25]    = "Y"
    t-labels[26] = "Frgn ODBC Drvr"            /* V7 */   /* t-add-lb &       */
    t-add-lb[26] = "Vendor"                               /* t-ver-lb for each*/
    t-on7[26]    = "Y"
    t-labels[27] = "DataServer"
    t-add-lb[27] = "DB Engine"
    t-labels[28] = ""                                                /* SPARE */
    t-labels[29] = ""                                                /* SPARE */
    t-labels[30] = ""                                                /* SPARE */
    t-labels[31] = "".                                               /* SPARE */
    ASSIGN                                              /* PROGRESS Utilities */
    t-labels[32] = "Installation"
    t-labels[33] = "Probuild"
    t-labels[34] = "Proutil"
    t-add-lb[34] = "-C Option"
    t-labels[35] = ""                                                /* SPARE */
    t-labels[36] = ""                                                /* SPARE */
    t-labels[37] = ""                                                /* SPARE */
    t-labels[38] = ""                                                /* SPARE */
    t-labels[39] = "Other"                                     /* t-addnl[39] */
    t-labels[40] = "".                                               /* SPARE */
    ASSIGN                                             /* Report Category (1) */
    t-labels[41] = "Installation"
    t-labels[42] = "Operating Environment"
    t-labels[43] = "Application Environment"
    t-labels[44] = "Enhancement Request"
    t-labels[45] = "Documentation Related"
    t-labels[46] = "Client/Server Networking Related"
    t-labels[47] = "Other"
    t-labels[48] = ""                                                /* SPARE */
    t-labels[49] = ""                                                /* SPARE */
    t-labels[50] = "".                                               /* SPARE */
    ASSIGN                                             /* Report Category (2) */
    t-labels[51] = "Bad Media"                                /* Installation */
    t-add-lb[51] = "Batch#"
    t-ver-lb[51] = "Prod"
    t-msg-lb[51] = "Error/#"
    t-labels[52] = "Config. Issue"
    t-labels[53] = "Ran OK before"                             /* Application */
    t-add-lb[53] = "Ran OK on which Vers?"
    t-labels[54] = "Tested w/Debug"
    t-labels[55] = "Seen w/UIB/Edt"
    t-add-lb[55] = "Which? UIB/Editor/BOTH"
    t-add-lb[45] = "Title"                                   /* Documentation */
    t-ver-lb[45] = "Page #"
    t-msg-lb[45] = "Description"
    t-labels[56] = "TCP/IP"                                     /* Networking */
    t-add-lb[56] = "Product"
    t-labels[57] = "Netbios"
    t-add-lb[57] = "Product"
    t-labels[58] = "SPX"
    t-ver-lb[58] = "IPX/SPX vrsn"
    t-labels[59] = "DDE"
    t-labels[60] = ""                                                /* SPARE */
    t-labels[61] = "Token-Ring"
    t-add-lb[61] = "NIC Type"
    t-ver-lb[61] = "DRVR vrsn"
    t-labels[62] = "Ethernet"
    t-add-lb[62] = "NIC Type"
    t-ver-lb[62] = "DRVR vrsn"
    t-labels[63] = "ARCnet"
    t-add-lb[63] = "NIC Type"
    t-ver-lb[63] = "DRVR vrsn"
    t-labels[64] = ""                                                /* SPARE */
    t-labels[65] = "PROGRESS configuration is part of WAN/Internet"
    t-labels[66] = "Error received by Server"
    t-msg-lb[66] = "Err Msg"
    t-labels[67] = "Error received by Client"
    t-msg-lb[67] = "Err Msg"
    t-labels[68] = ""                                                /* SPARE */
    t-labels[69] = ""                                                /* SPARE */
    t-labels[70] = "".                                               /* SPARE */

    FORM
    "Please mark the product area(s) related to this report:"            SKIP(1)
    "PROGRESS TOOLS"                                                     SKIP(1)
    t-topics[1]  AT  5 t-labels[1]        t-topics[2]  AT 30
    t-labels[2]        t-topics[3]  AT 55 t-labels[3]                    SKIP
    t-topics[4]  AT  5 t-labels[4]        t-topics[5]  AT 30
    t-labels[5]        t-topics[6]  AT 55 t-labels[6]                    SKIP
    t-topics[7]  AT  5 t-labels[7]        t-topics[8]  AT 30
    t-labels[8]        t-topics[9]  AT 55 t-labels[9]                    SKIP
    t-topics[10] AT  5 t-labels[10]       t-topics[11] AT 30
    t-labels[11]       t-topics[12] AT 55 t-labels[12]                   SKIP
    t-topics[13] AT  5 t-labels[13]       t-topics[14] AT 30
    t-labels[14]       t-topics[15] AT 55 t-labels[15]                   SKIP
    t-topics[16] AT  5 t-labels[16]       t-topics[17] AT 30
    t-labels[17]       t-topics[18] AT 55 t-labels[18]                   SKIP(1)
    WITH FRAME p-tools
    NO-LABELS WIDTH 78 CENTERED ROW 5 TITLE " PRODUCT AREA - 1 ".
    FORM
    "Please mark the product area(s) related to this report:"            SKIP(1)
    "OPTIONAL PRODUCTS"                                                  SKIP(1)
    t-topics[19]       t-labels[19]        t-topics[20] AT 33  
    t-labels[20]       t-topics[21] AT 57  t-labels[21]                  SKIP
    t-topics[22]       t-labels[22]        t-topics[23] AT 33
    t-labels[23]       t-topics[24] AT 57  t-labels[24]                  SKIP(1)
    t-topics[25]       t-labels[25]        t-add-lb[25] FORMAT "x(6)"
                       t-addnl[25]         t-ver-lb[25] FORMAT "x(3)" 
                                           t-versn[25]      SKIP
    t-topics[26]       t-labels[26]        t-add-lb[26] FORMAT "x(6)"
                       t-addnl[26]         t-ver-lb[26] FORMAT "x(3)" 
                                           t-versn[26]      SKIP
    t-topics[27]       t-labels[27]        t-add-lb[27] FORMAT "x(6)"
                       t-addnl[27]         t-ver-lb[27] FORMAT "x(3)" 
                                           t-versn[27]      SKIP
    t-topics[28]       t-labels[28]        t-add-lb[28] FORMAT "x(6)"
                       t-addnl[28]         t-ver-lb[28] FORMAT "x(3)" 
                                           t-versn[28]      SKIP
    t-topics[29]       t-labels[29]        t-add-lb[29] FORMAT "x(6)"
                       t-addnl[29]         t-ver-lb[29] FORMAT "x(3)" 
                                           t-versn[29]      SKIP
    t-topics[30]       t-labels[30]        t-add-lb[30] FORMAT "x(6)"
                       t-addnl[30]         t-ver-lb[30] FORMAT "x(3)" 
                                           t-versn[30]      SKIP
    t-topics[31]       t-labels[31]        t-add-lb[31] FORMAT "x(6)"
                       t-addnl[31]         t-ver-lb[31] FORMAT "x(3)" 
                                           t-versn[31]      SKIP(1)
    WITH FRAME p-optn
    NO-LABELS WIDTH 78 CENTERED ROW 5 TITLE " PRODUCT AREA - 2 ".
    FORM
    "Please mark the product area(s) related to this report:"            SKIP(1)
    "PROGRESS Utilities"                                                 SKIP(1)
    t-topics[32] AT  1 t-labels[32]                                      SKIP
    t-topics[33] AT  1 t-labels[33]      
    t-topics[34] AT  1 t-labels[34]        t-add-lb[34]   FORMAT "x(9)"
                                           t-addnl[34]    FORMAT "x(40)" SKIP
    t-topics[35] AT  1 t-labels[35]        t-add-lb[35]   FORMAT "x(9)"
                                           t-addnl[35]    FORMAT "x(40)" SKIP
    t-topics[36] AT  1 t-labels[36]        t-add-lb[36]   FORMAT "x(9)"
                                           t-addnl[36]    FORMAT "x(40)" SKIP
    t-topics[37] AT  1 t-labels[37]        t-add-lb[37]   FORMAT "x(9)"
                                           t-addnl[37]    FORMAT "x(40)" SKIP
    t-topics[38] AT  1 t-labels[38]        t-add-lb[38]   FORMAT "x(9)"
                                           t-addnl[38]    FORMAT "x(40)" SKIP
    t-labels[39] AT  1 t-addnl[39] FORMAT "x(60)"                        SKIP
    t-labels[40] AT  1 t-addnl[40] FORMAT "x(60)"                        SKIP(1)
    WITH FRAME p-util
    NO-LABELS WIDTH 78 CENTERED ROW 5 TITLE " PRODUCT AREA - 3 ".
    FORM
    "Please mark ONE of the following TYPES of issues:"                  SKIP
    "(Complete further information for that ONE issue if necessary) "    SKIP(1)
    t-topics[41] AT  20 t-labels[41] FORMAT "x(34)"                      SKIP
    t-topics[42] AT  20 t-labels[42] FORMAT "x(34)"                      SKIP
    t-topics[43] AT  20 t-labels[43] FORMAT "x(34)"                      SKIP
    t-topics[44] AT  20 t-labels[44] FORMAT "x(34)"                      SKIP
    t-topics[45] AT  20 t-labels[45] FORMAT "x(34)"                      SKIP
    t-topics[46] AT  20 t-labels[46] FORMAT "x(34)"                      SKIP
    t-topics[47] AT  20 t-labels[47] FORMAT "x(34)"                      SKIP
    t-topics[48] AT  20 t-labels[48] FORMAT "x(34)"                      SKIP
    t-topics[49] AT  20 t-labels[49] FORMAT "x(34)"                      SKIP
    t-topics[50] AT  20 t-labels[50] FORMAT "x(34)"                      SKIP(1)
    WITH FRAME p-report1
    NO-LABELS WIDTH 78 CENTERED ROW 5 TITLE " TYPE OF ISSUE ".
    FORM                                                                 SKIP(1)
    t-topics[51] AT 1 t-labels[51]      t-add-lb[51] AT 21 t-addnl[51]
    t-ver-lb[51]      t-versn[51]       t-msg-lb[51] AT 21 t-messg[51]   SKIP
    t-topics[52] AT 1 t-labels[52]                                       SKIP(1)
    WITH FRAME rpt-41
    NO-LABELS OVERLAY ROW 8 CENTERED TITLE " INSTALLATION DETAIL ".
    FORM                                                                 SKIP(1)
    t-topics[53] AT 5 t-labels[53] t-add-lb[53] FORMAT "x(22)" AT 25 t-addnl[53]
    t-topics[54] AT 5 t-labels[54]
    t-topics[55] AT 5 t-labels[55] t-add-lb[55] FORMAT "x(22)" AT 25 t-addnl[55]
    "                                                                  " SKIP
    WITH FRAME rpt-43
    NO-LABELS OVERLAY ROW 9 CENTERED TITLE " APPLICATION ENVIRONMENT DETAIL ".
    FORM                                                                 SKIP(1)
    t-add-lb[45] FORMAT "x(5)"   TO 16 t-addnl[45] FORMAT "x(58)"        SKIP
    t-ver-lb[45] FORMAT "x(6)"   TO 16 t-versn[45]                       SKIP
    t-msg-lb[45] FORMAT "x(11)"  TO 16 t-messg[45] FORMAT "x(58)"        SKIP(1)
    WITH FRAME rpt-45
    NO-LABELS OVERLAY ROW 10 CENTERED TITLE " DOCUMENTATION ISSUE DETAIL ".
    FORM "PROTOCOL"
    "---------------------------------------------------------------------"
    SKIP
    t-topics[56]      t-labels[56] t-add-lb[56]   t-addnl[56]
                                   t-ver-lb[56]   t-versn[56]        SKIP
    t-topics[57]      t-labels[57] t-add-lb[57]   t-addnl[57]
                                   t-ver-lb[57]   t-versn[57]        SKIP
    t-topics[58]      t-labels[58] t-ver-lb[58]   FORMAT "x(12)"
                                   t-versn[58]                           SKIP
    t-topics[59]      t-labels[59]                                       SKIP
    t-topics[60]      t-labels[60] t-add-lb[60]   t-addnl[60]
                                   t-ver-lb[60]   t-versn[60]        SKIP
    "TOPOLOGY"
    "---------------------------------------------------------------------"
    SKIP
    t-topics[61]      t-labels[61] t-add-lb[61]   FORMAT "x(8)"
                      t-addnl[61] t-ver-lb[61] FORMAT "x(3)" t-versn[61] SKIP
    t-topics[62]      t-labels[62] t-add-lb[62]   FORMAT "x(8)"
                      t-addnl[62] t-ver-lb[62] FORMAT "x(3)" t-versn[62] SKIP
    t-topics[63]      t-labels[63] t-add-lb[63]   FORMAT "x(8)"
                      t-addnl[63] t-ver-lb[63] FORMAT "x(3)" t-versn[63] SKIP
    t-topics[64]      t-labels[64] t-add-lb[64]   FORMAT "x(8)"
                      t-addnl[64] t-ver-lb[64] FORMAT "x(3)" t-versn[64] SKIP
    t-topics[65]      t-labels[65]             FORMAT "x(54)"            SKIP 
    "DETAILS"
    "----------------------------------------------------------------------"
    SKIP
    t-topics[66]      t-labels[66] FORMAT "x(32)" t-add-lb[66] t-addnl[66] SKIP
    t-topics[67]      t-labels[67] FORMAT "x(32)" t-add-lb[67] t-addnl[67] SKIP
    WITH FRAME rpt-46
    NO-LABELS OVERLAY CENTERED TITLE " CLIENT/SERVER NETWORKING DETAIL ".
    FORM SKIP(1) t-addnl[47] FORMAT "x(70)" SKIP(1)
    WITH FRAME rpt-47 NO-LABELS OVERLAY ROW 11 CENTERED TITLE " OTHER ISSUE ".

    blockq:
    DO ON ERROR UNDO, LEAVE:

        ASSIGN                                          /* Check if Version 7 */
        tx7 =
        IF   INTEGER(SUBSTRING(prog-vers,1,1))            >= 7           OR
           ( SUBSTRING(prog-vers,1,1)                      = "1"     AND
             INDEX("1234567890",SUBSTRING(prog-vers,2,1))  > 0       )
        THEN YES
        ELSE NO.

        blockqq: DO WHILE TRUE:
            DO i = 1 TO 18:
                DISPLAY t-labels[i]  WHEN t-labels[i] <> "" WITH FRAME p-tools.
            END.
            UPDATE  t-topics[1]  WHEN t-labels[1] <> ""
                    t-topics[2]  WHEN t-labels[2] <> ""
                    t-topics[3]  WHEN t-labels[3] <> ""
                    t-topics[4]  WHEN t-labels[4] <> ""
                    t-topics[5]  WHEN t-labels[5] <> ""
                    t-topics[6]  WHEN t-labels[6] <> ""
                    t-topics[7]  WHEN t-labels[7] <> ""
                    t-topics[8]  WHEN t-labels[8] <> "" /*Remove t-on7 & tx7*/
                    t-topics[9]  WHEN t-labels[9] <> "" /*whn V7 tst not need*/
                    t-topics[10] WHEN t-labels[10] <> "" AND
                                      t-on7[10] = "Y"    AND tx7 = YES
                    t-topics[11] WHEN t-labels[11] <> "" AND
                                      t-on7[11] = "Y"    AND tx7 = YES
                    t-topics[12] WHEN t-labels[12] <> "" AND
                                      t-on7[12] = "Y"    AND tx7 = YES
                    t-topics[13] WHEN t-labels[13] <> "" AND
                                      t-on7[13] = "Y"    AND tx7 = YES
                    t-topics[14] WHEN t-labels[14] <> "" AND
                                      t-on7[14] = "Y"    AND tx7 = YES
                    t-topics[15] WHEN t-labels[15] <> "" AND
                                      t-on7[15] = "Y"    AND tx7 = YES
                    t-topics[16] WHEN t-labels[16] <> "" AND
                                      t-on7[16] = "Y"    AND tx7 = YES
                    t-topics[17] WHEN t-labels[17] <> "" AND
                                      t-on7[17] = "Y"    AND tx7 = YES
                    t-topics[18] WHEN t-labels[18] <> "" AND
                                      t-on7[18] = "Y"    AND tx7 = YES
                    WITH FRAME p-tools.
            HIDE FRAME p-tools NO-PAUSE.

            DO i = 19 TO 31 WITH FRAME p-optn:
                DISPLAY t-labels[i]  WHEN t-labels[i] <> "".
                IF I > 24 THEN
                DISPLAY t-add-lb[i]  WHEN t-labels[i] <> ""
                        t-ver-lb[i]  WHEN t-labels[i] <> "".
            END.
            UPDATE t-topics[19]  WHEN t-labels[19] <> "" /*REMOVE t-on7 & tx7*/
                   t-topics[20]  WHEN t-labels[20] <> "" /*when V7 test      */
                   t-topics[21]  WHEN t-labels[21] <> "" /*no longer needed  */
                   t-topics[22]  WHEN t-labels[22] <> ""
                   t-topics[23]  WHEN t-labels[23] <> ""
                   t-topics[24]  WHEN t-labels[24] <> ""
                   t-topics[25]  WHEN t-labels[25] <> "" AND
                                      t-on7[25] = "Y"    AND tx7 = YES
                   t-addnl[25]   WHEN t-labels[25] <> "" AND
                                      t-on7[25] = "Y"    AND tx7 = YES
                   t-versn[25]   WHEN t-labels[25] <> "" AND
                                      t-on7[25] = "Y"    AND tx7 = YES
                   t-topics[26]  WHEN t-labels[26] <> "" AND
                                      t-on7[26] = "Y"    AND tx7 = YES
                   t-addnl[26]   WHEN t-labels[26] <> "" AND
                                      t-on7[26] = "Y"    AND tx7 = YES
                   t-versn[26]   WHEN t-labels[26] <> "" AND
                                      t-on7[26] = "Y"    AND tx7 = YES
                   t-topics[27]  WHEN t-labels[27] <> ""
                   t-addnl[27]   WHEN t-labels[27] <> ""
                   t-versn[27]   WHEN t-labels[27] <> ""
                   t-topics[28]  WHEN t-labels[28] <> "" AND
                                      t-on7[28] = "Y"    AND tx7 = YES
                   t-addnl[28]   WHEN t-labels[28] <> "" AND
                                      t-on7[28] = "Y"    AND tx7 = YES
                   t-versn[28]   WHEN t-labels[28] <> "" AND
                                      t-on7[28] = "Y"    AND tx7 = YES
                   t-topics[29]  WHEN t-labels[29] <> "" AND
                                      t-on7[29] = "Y"    AND tx7 = YES
                   t-addnl[29]   WHEN t-labels[29] <> "" AND
                                      t-on7[29] = "Y"    AND tx7 = YES
                   t-versn[29]   WHEN t-labels[29] <> "" AND
                                      t-on7[29] = "Y"    AND tx7 = YES
                   t-topics[30]  WHEN t-labels[30] <> "" AND
                                      t-on7[30] = "Y"    AND tx7 = YES
                   t-addnl[30]   WHEN t-labels[30] <> "" AND
                                      t-on7[30] = "Y"    AND tx7 = YES
                   t-versn[30]   WHEN t-labels[30] <> "" AND
                                      t-on7[30] = "Y"    AND tx7 = YES
                   t-topics[31]  WHEN t-labels[31] <> "" AND
                                      t-on7[31] = "Y"    AND tx7 = YES
                   t-addnl[31]   WHEN t-labels[31] <> "" AND
                                      t-on7[31] = "Y"    AND tx7 = YES
                   t-versn[31]   WHEN t-labels[31] <> "" AND
                                      t-on7[31] = "Y"    AND tx7 = YES
                   WITH FRAME p-optn.
            HIDE FRAME p-optn NO-PAUSE.

            DO i = 32 TO 40 WITH FRAME p-util:
                DISPLAY t-labels[i]  WHEN t-labels[i] <> "".
                IF i > 33 AND i < 39 THEN
                DISPLAY t-add-lb[i]  WHEN t-labels[i] <> "".
            END.
            UPDATE
            t-topics[32]  WHEN t-labels[32] <> ""
            t-topics[33]  WHEN t-labels[33] <> ""
            t-topics[34]  WHEN t-labels[34] <> ""
            t-addnl[34]   WHEN t-labels[34] <> ""
            t-topics[35]  WHEN t-labels[35] <> ""
            t-addnl[35]   WHEN t-labels[35] <> ""
            t-topics[36]  WHEN t-labels[36] <> ""
            t-addnl[36]   WHEN t-labels[36] <> ""
            t-topics[37]  WHEN t-labels[37] <> ""
            t-addnl[37]   WHEN t-labels[37] <> ""
            t-topics[38]  WHEN t-labels[38] <> ""
            t-addnl[38]   WHEN t-labels[38] <> ""
            t-addnl[39]   WHEN t-labels[39] <> ""
            t-addnl[40]   WHEN t-labels[40] <> ""
            WITH FRAME p-util.
            IF t-addnl[39] <> "" THEN ASSIGN t-topics[39] = "x".
            IF t-addnl[40] <> "" THEN ASSIGN t-topics[40] = "x".
            HIDE FRAME p-util NO-PAUSE.

            INPUT CLEAR.
            DO i = 41 TO 50 WITH FRAME p-report1:
                DISPLAY t-labels[i] WHEN t-labels[i] <> "".
            END.

            DO txcnt = 41 TO 50 WITH FRAME p-report1:
                IF txcnt < 50 THEN IF t-labels[txcnt] <> ""
                THEN DISPLAY (txcnt - 40) FORMAT "9" @ t-topics[txcnt].
                ELSE IF t-labels[50] <> ""
                THEN DISPLAY "x" @ t-topics[50].
            END.
            MESSAGE "Enter Selection # or RETURN to skip.".

            READKEY.
            HIDE MESSAGE NO-PAUSE.

            ASSIGN txcnt = INDEX("123456789x",CHR(LASTKEY)).
            IF txcnt = 0  THEN LEAVE blockqq.

            ASSIGN txcnt           = txcnt + 40
                   t-topics[txcnt] = "x".
            HIDE FRAME p-report1 NO-PAUSE.

            IF t-topics[41] <> ""
            THEN DO WITH FRAME rpt-41:
                DISPLAY t-topics[51] t-labels[51] t-add-lb[51] t-ver-lb[51]
                t-msg-lb[51] t-labels[52].
                UPDATE
                t-topics[51] t-addnl[51]  t-versn[51] t-messg[51] t-topics[52].
                HIDE FRAME rpt-41 NO-PAUSE.
            END.
            IF t-topics[43] <> ""
            THEN DO WITH FRAME rpt-43:
                DISPLAY
               t-labels[53] t-add-lb[53] t-labels[54] t-labels[55] t-add-lb[55].
                UPDATE
                t-topics[53] t-addnl[53] t-topics[54] t-topics[55] t-addnl[55].
                HIDE FRAME rpt-43 NO-PAUSE.
            END.
            IF t-topics[45] <> ""
            THEN DO WITH FRAME rpt-45:
                DISPLAY t-add-lb[45] t-ver-lb[45] t-msg-lb[45].
                UPDATE t-addnl[45] t-versn[45] t-messg[45].
                HIDE FRAME rpt-45 NO-PAUSE.
            END.
            IF t-topics[46] <> ""
            THEN DO WITH FRAME rpt-46:
                DO i = 56 TO 67:
                    DISPLAY t-labels[i]  WHEN t-labels[i] <> "".
                    IF i <> 58 AND i <> 59 AND i <> 65  THEN
                    DISPLAY t-add-lb[i]  WHEN t-labels[i] <> "".
                    IF i <> 59 AND i < 65 THEN
                    DISPLAY t-ver-lb[i]  WHEN t-labels[i] <> "".
                END.
                UPDATE t-topics[56]  WHEN t-labels[56] <> ""
                       t-addnl[56]   WHEN t-labels[56] <> ""
                       t-versn[56]   WHEN t-labels[56] <> ""
                       t-topics[57]  WHEN t-labels[57] <> ""
                       t-addnl[57]   WHEN t-labels[57] <> ""
                       t-versn[57]   WHEN t-labels[57] <> ""
                       t-topics[58]  WHEN t-labels[58] <> ""
                       t-versn[58]   WHEN t-labels[58] <> ""
                       t-topics[59]  WHEN t-labels[59] <> ""
                       t-topics[60]  WHEN t-labels[60] <> ""
                       t-addnl[60]   WHEN t-labels[60] <> ""
                       t-versn[60]   WHEN t-labels[60] <> ""
                       t-topics[61]  WHEN t-labels[61] <> ""
                       t-addnl[61]   WHEN t-labels[61] <> ""
                       t-versn[61]   WHEN t-labels[61] <> ""
                       t-topics[62]  WHEN t-labels[62] <> ""
                       t-addnl[62]   WHEN t-labels[62] <> ""
                       t-versn[62]   WHEN t-labels[62] <> ""
                       t-topics[63]  WHEN t-labels[63] <> ""
                       t-addnl[63]   WHEN t-labels[63] <> ""
                       t-versn[63]   WHEN t-labels[63] <> ""
                       t-topics[64]  WHEN t-labels[64] <> ""
                       t-addnl[64]   WHEN t-labels[64] <> ""
                       t-versn[64]   WHEN t-labels[64] <> ""
                       t-topics[65]  WHEN t-labels[65] <> ""
                       t-topics[66]  WHEN t-labels[66] <> ""
                       t-addnl[66]   WHEN t-labels[66] <> ""
                       t-topics[67]  WHEN t-labels[67] <> ""
                       t-addnl[67]   WHEN t-labels[67] <> "".
                HIDE FRAME rpt-46 NO-PAUSE.
            END.
            IF t-topics[47] <> ""
            THEN DO:
                UPDATE t-addnl[47] WITH FRAME rpt-47.
                HIDE FRAME rpt-47 NO-PAUSE.
            END.
            LEAVE blockqq.
        END. /* blockqq */

        OUTPUT TO VALUE(outfile).                             /* open output */

        DISPLAY callno tdate rec-name sender-name snd-company send-email
        WITH FRAME emhead.

        DISPLAY cur-status urgent follow-up bug prog-prod prog-vers prog-sn
        memory users op-sys overs machine other WITH FRAME eml-dtl.

        PUT "                                                            " SKIP
        "----------------------- Report Categories ----------------------- " +
        txrev FORMAT "x(71)" AT 7                                          SKIP
        "                                                                " SKIP.
        DO txcnt = 1 TO 70:
            IF t-topics[txcnt] <> ""
            THEN DO:

                PUT UNFORMATTED t-labels[txcnt] SKIP.
                IF t-addnl[txcnt] <> ""
                THEN
                IF t-add-lb[txcnt] <> ""
                THEN PUT UNFORMATTED t-add-lb[txcnt] AT 3 ": "
                                     t-addnl[txcnt]                        SKIP.
                ELSE PUT UNFORMATTED t-addnl[txcnt]  AT 3                  SKIP.

                IF t-versn[txcnt] <> ""
                THEN
                IF t-ver-lb[txcnt] <> ""
                THEN PUT UNFORMATTED t-ver-lb[txcnt] AT 3      ": "
                                     t-versn[txcnt]                        SKIP.
                ELSE PUT UNFORMATTED t-versn[txcnt]  AT 3                  SKIP.

                IF t-messg[txcnt] <> ""
                THEN
                IF t-msg-lb[txcnt] <> ""
                THEN PUT UNFORMATTED t-msg-lb[txcnt] AT 3      ": "
                                     t-messg[txcnt]                        SKIP.
                ELSE PUT UNFORMATTED t-messg[txcnt]  AT 3                  SKIP.
            END.
        END.                           /* output */
        UPDATE TEXT(desc1)                         WITH FRAME det1.
        IF desc1[15] NE "" THEN UPDATE TEXT(desc2) WITH FRAME det2.
        OUTPUT CLOSE.
    END. /* blockq */
    /**** END QUESTIONAIRE SECTION ********************************************/
END.

/**** NOTE TO SITE ************************************************************/
/**** THE FOLLOWING FRAME W/PAUSE MAY NEED TO BE MODIFIED OR ELIMINATED IF ****/
/**** YOU IMPLEMENT A DIRECT CALL TO YOUR OS TO INVOKE YOUR MAIL ROUTINE.  ****/
/****                                                                      ****/
/**** ALSO: IF YOU ADD ANYTHING TO YOUR E-MAIL, BE SURE YOU PLACE IT AFTER ****/
/****       THE CONTENTS OF email.txt.  IN OTHER WORDS, NOTHING SHOULD     ****/
/****       APPEAR BEFORE IT EXCEPT THE STANDARD INTERNET CONTROL LINES.   ****/

DO WITH FRAME reminder-fr CENTERED ROW 5
   TITLE " TROUBLESHOOTING REPORT COMPLETE ":
    DISPLAY
    "                                                                   "
    " A file named 'email.txt' has been generated to be emailed to PSC. "
    " Please use your email tool to send file email.txt to              "
    "                                                                   "
    "                   support@progress.com                            "
    "                                                                   "
    "                                                                   ".
END.
PAUSE 5 NO-MESSAGE.
/**** NOTE TO SITE *************************************************************
***** THE FOLLOWING CODE MIGHT BE USED IF YOUR ARE ON A UNIX SYSTEM ***********/
IF OPSYS = "UNIX"
THEN DO:
    MESSAGE "Sending query to PROGRESS Technical Support".
    UNIX SILENT mail support@progress.com < email.txt.
END.
/* END OF PROCEDURE */
