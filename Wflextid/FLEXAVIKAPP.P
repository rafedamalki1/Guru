/*FLEXAVIKAPP.P*/
&Scoped-define NEW    
&Scoped-define SHARED 
{FLEXTAB.I}

PROCEDURE nydag_UI :
   DEFINE INPUT PARAMETER vem AS CHARACTER NO-UNDO.
   DEFINE INPUT PARAMETER regdatum AS DATE NO-UNDO.
   DEFINE OUTPUT PARAMETER TABLE FOR flexdagtemp.
   EMPTY TEMP-TABLE flexdagtemp NO-ERROR. 
   FIND FIRST FLEXDAG WHERE FLEXDAG.PERSONALKOD = vem AND
   FLEXDAG.DATUM = regdatum USE-INDEX FLEX NO-LOCK NO-ERROR. 
   IF AVAILABLE FLEXDAG THEN DO:
      CREATE flexdagtemp.
      BUFFER-COPY FLEXDAG TO flexdagtemp.
      flexdagtemp.FREC = RECID(FLEXDAG).
   END.
END PROCEDURE.

PROCEDURE nyflextemp_UI :
   DEFINE INPUT PARAMETER vem AS CHARACTER NO-UNDO.
   DEFINE INPUT PARAMETER regdatum AS DATE NO-UNDO.
   DEFINE OUTPUT PARAMETER TABLE FOR flextemp.
   EMPTY TEMP-TABLE flextemp NO-ERROR.    
   OPEN QUERY fq FOR EACH FLEXTID WHERE FLEXTID.PERSONALKOD = vem AND
   FLEXTID.DATUM = regdatum  USE-INDEX FLEX NO-LOCK.    
   GET FIRST fq NO-LOCK.
   DO WHILE AVAILABLE(FLEXTID):
      CREATE flextemp.
      BUFFER-COPY FLEXTID TO flextemp.
      flextemp.FREC = RECID(FLEXTID).
      GET NEXT fq NO-LOCK.
   END.   
END PROCEDURE.

PROCEDURE bortflex_UI :
   DEFINE INPUT PARAMETER TABLE FOR flextemp.
   DO TRANSACTION:
      FIND FIRST flextemp NO-ERROR.
      FIND FIRST FLEXTID WHERE RECID(FLEXTID) = flextemp.FREC 
      EXCLUSIVE-LOCK NO-ERROR.
      IF AVAILABLE FLEXTID THEN DELETE FLEXTID.   
      /* Om det inte finns någon registrering kvar - plocka bort FLEXDAG*/
      FIND FIRST FLEXTID WHERE FLEXTID.PERSONALKOD = flextemp.PERSONALKOD AND 
      FLEXTID.DATUM = flextemp.DATUM NO-ERROR. 
      IF NOT AVAILABLE FLEXTID THEN DO:
         FIND FIRST FLEXDAG WHERE FLEXDAG.PERSONALKOD = flextemp.PERSONALKOD AND 
         FLEXDAG.DATUM = flextemp.DATUM EXCLUSIVE-LOCK NO-ERROR. 
         IF AVAILABLE FLEXDAG THEN DELETE FLEXDAG.
         FOR EACH TIDREGITAB WHERE TIDREGITAB.PERSONALKOD = flextemp.PERSONALKOD AND 
         TIDREGITAB.DATUM = flextemp.DATUM AND TIDREGITAB.TIDLOG = TRUE 
         AND TIDREGITAB.OVERTIDUTTAG = "F"  EXCLUSIVE-LOCK:
            IF TIDREGITAB.GODKAND BEGINS "G" OR TIDREGITAB.GODKAND BEGINS "F"  THEN.
            ELSE DELETE TIDREGITAB.
         END.
   
      END.
   END.
   RELEASE FLEXTID NO-ERROR.
   RELEASE FLEXDAG NO-ERROR.
   RELEASE TIDREGITAB NO-ERROR.
END PROCEDURE.

PROCEDURE flexdagok_IU:
   DEFINE INPUT PARAMETER TABLE FOR flexdagtemp.
   FIND FIRST flexdagtemp NO-ERROR.
   DO TRANSACTION:
      FIND FIRST FLEXDAG WHERE RECID(FLEXDAG) = flexdagtemp.FREC 
      EXCLUSIVE-LOCK NO-ERROR.
      IF AVAILABLE FLEXDAG THEN BUFFER-COPY flexdagtemp TO FLEXDAG.
   END.
      
   RELEASE FLEXDAG NO-ERROR.
END PROCEDURE.
