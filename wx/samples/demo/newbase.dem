/************************************************************************************
        PROCEDURE: newbase.dem

        PURPOSE:   Demonstration program for "newbase" tool

        SYNTAX:    "RUN samples/demo/newbase.dem"

        REMARKS:   This program shows some samples of the tools
                   abilities and then allows the user to try some
                   on his/her own.

        PARAMETERS:NONE

        AUTHORS:   Progress Consulting
        DATE:      March 1993

        LAST INSPECTED:
        INSPECTED BY:

 ************************************************************************************/
 /* Copyright(c) PROGRESS SOFTWARE CORPORATION, 1993 - All Rights Reserved.            */


/*Code_Start*/

DEFINE VARIABLE dnumber AS INTEGER   NO-UNDO.
DEFINE VARIABLE newbase AS INTEGER   NO-UNDO.
DEFINE VARIABLE nstring AS CHARACTER NO-UNDO.

DEFINE BUTTON bQUIT LABEL "EXIT" AUTO-ENDKEY.
DEFINE BUTTON bOK   LABEL "OK"   AUTO-GO.

DEFINE VARIABLE doutput AS CHARACTER EXTENT 16 FORMAT "x(18)" NO-UNDO.

FORM 
  SKIP(1)
  dnumber LABEL "Decimal number" 
  SPACE(2) bOK SPACE(2) bQUIT
  WITH SIDE-LABELS FRAME sample VIEW-AS DIALOG-BOX 
  TITLE "Try One".
  
ASSIGN
   FRAME sample:ROW = 4
   FRAME sample:COLUMN = 15.
   
VIEW FRAME sample.
DISPLAY
  nstring FORMAT "x(64)" SKIP(1)
  " 2:" doutput[ 2]  " 7:" doutput[ 7]  "12:" doutput[12] SKIP
  " 3:" doutput[ 3]  " 8:" doutput[ 8]  "13:" doutput[13] SKIP
  " 4:" doutput[ 4]  " 9:" doutput[ 9]  "14:" doutput[14] SKIP
  " 5:" doutput[ 5]  "10:" doutput[10]  "15:" doutput[15] SKIP
  " 6:" doutput[ 6]  "11:" doutput[11]  "16:" doutput[16] SKIP
  WITH FRAME base NO-LABELS.

ENABLE dnumber bOK bQUIT WITH FRAME sample.

REPEAT:
   UPDATE dnumber WITH FRAME sample.
   DISPLAY STRING(dnumber) + 
           " base 10 equals: (in bases 2 through 16)"
           @ nstring WITH FRAME base.
   DO newbase = 2 TO 16:
      RUN samples/newbase.p 
          (INPUT dnumber,INPUT newbase,OUTPUT nstring).
      DISPLAY 
        (IF LENGTH(nstring) > 18 THEN ? ELSE nstring) @ doutput[newbase]
         WITH FRAME base.
   END. /* DO newbase = 2 TO 16 */
END. /* REPEAT */

HIDE FRAME base   NO-PAUSE.






