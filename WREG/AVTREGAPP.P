/*
     Filename: AVTREGAPP.P
      Created: 2003.10.03 13:19ELPAO     
     Modified: 
*/
&SCOPED-DEFINE NEW 
&SCOPED-DEFINE SHARED 
{AVTALTEMP.I}

PROCEDURE laddaavt_UI:
   DEFINE OUTPUT PARAMETER TABLE FOR avtaltemp.
   FOR EACH AVTAL :         
      CREATE avtaltemp.
      BUFFER-COPY AVTAL TO avtaltemp.
   END.
END PROCEDURE.

PROCEDURE skapaavtal_UI:
   DEFINE OUTPUT PARAMETER avtid AS INTEGER NO-UNDO.
   DEFINE VARIABLE tempi AS INTEGER NO-UNDO.
   FIND LAST AVTAL USE-INDEX ID NO-LOCK NO-ERROR.
   IF AVAILABLE AVTAL THEN DO: 
      tempi = AVTAL.AVTALID.
      FIND LAST AVTAL USE-INDEX ORD NO-LOCK NO-ERROR.
      IF tempi >= AVTAL.ORDNING THEN avtid = tempi + 1.
      ELSE avtid = AVTAL.ORDNING + 1.
   END.
   ELSE avtid = 1.
   
   CREATE AVTAL.
   ASSIGN
   AVTAL.AVTALID = avtid
   AVTAL.ORDNING = avtid.
   RELEASE AVTAL.
END PROCEDURE.

PROCEDURE sparaavtal_UI:
   DEFINE INPUT PARAMETER avtid AS INTEGER NO-UNDO.
   DEFINE INPUT PARAMETER TABLE FOR avtaltemp.
   IF avtid NE ? THEN DO:
      FIND FIRST avtaltemp WHERE avtaltemp.AVTALID = avtid NO-LOCK NO-ERROR.
      IF AVAILABLE avtaltemp THEN DO TRANSACTION:
         FIND FIRST AVTAL WHERE AVTAL.AVTALID = avtaltemp.AVTALID EXCLUSIVE-LOCK NO-ERROR.
         IF AVAILABLE AVTAL THEN DO:
            BUFFER-COPY avtaltemp TO AVTAL.
         END.
      END.
      RELEASE AVTAL.
   END.
   ELSE DO:
      FOR EACH avtaltemp:         
         DO TRANSACTION:
            FIND FIRST AVTAL WHERE AVTAL.AVTALID = avtaltemp.AVTALID EXCLUSIVE-LOCK NO-ERROR.
            IF AVAILABLE AVTAL THEN DO:
               AVTAL.ORDNING = avtaltemp.ORDNING.
            END.
         END.
         RELEASE AVTAL.
      END.
   END.
END PROCEDURE.

PROCEDURE avtcheck_UI:
   DEFINE INPUT PARAMETER avtid AS INTEGER NO-UNDO.
   DEFINE OUTPUT PARAMETER felmedd AS CHARACTER NO-UNDO.
   
   felmedd = "".
   FIND FIRST AVTAL WHERE AVTAL.AVTALID = avtid NO-LOCK NO-ERROR.
   IF AVAILABLE AVTAL THEN DO:
      FIND FIRST AVTAONR WHERE AVTAONR.AVTALID = avtid NO-LOCK NO-ERROR.
      IF AVAILABLE AVTAONR THEN DO:
        felmedd = "Tillstånd/Avtal/Dokument: " + SUBSTRING(AVTAL.AVTAL,1,30) + 
           " går ej att ta bort. Det används i vissa projekt.".
      END.
   END.
END PROCEDURE.

PROCEDURE tabortavtal_UI:
   DEFINE INPUT PARAMETER avtid AS INTEGER NO-UNDO.

   DO TRANSACTION:
      FIND FIRST AVTAL WHERE AVTAL.AVTALID = avtid EXCLUSIVE-LOCK NO-ERROR.
      IF AVAILABLE AVTAL THEN DELETE AVTAL.
   END.
END PROCEDURE.
