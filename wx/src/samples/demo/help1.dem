/************************************************************************************
        PROCEDURE: help1.dem

        PURPOSE:   Demo program for applhelp.p

        SYNTAX:    "RUN samples/demo/help1.dem"

        REMARKS:   This code creates orders.  If an order is being created
                   and the user presses F1 applhelp.p is run.
 ************************************************************************************/
 /* Copyright(c) PROGRESS SOFTWARE CORPORATION, 1993 - All Rights Reserved.            */
/* Code Start */
  

/************* VARIABLES *****************/
DEFINE NEW SHARED VAR s_cust_num LIKE customer.cust-num NO-UNDO.
DEFINE NEW SHARED VAR s_carrier  LIKE order.carrier     NO-UNDO.
DEFINE NEW SHARED VAR s_date     AS DATE                NO-UNDO.
DEFINE NEW SHARED VAR s_cut      AS CHAR                NO-UNDO.

/************* BUTTONS   ****************/
DEFINE BUTTON btn-Save   LABEL "SAVE" AUTO-GO.
DEFINE BUTTON btn-Create LABEL "CREATE".
DEFINE BUTTON btn-Exit   LABEL "EXIT"   AUTO-ENDKEY.
DEFINE BUTTON btn-Cancel LABEL "CANCEL" AUTO-ENDKEY.
DEFINE BUTTON btn-Help   LABEL "HELP".
   
/************* FRAMES    ****************/
DEFINE FRAME frm-order
    order.order-num    COLON 20
    order.cust-num     COLON 20
    order.order-date   COLON 20
    order.promise-date COLON 20
    order.carrier      COLON 20
    order.instructions COLON 20
    btn-Create AT ROW 4 COL 50 btn-Exit AT ROW 6 COL 50
    WITH ROW 5 COL 2 SIDE-LABELS CENTERED OVERLAY NO-BOX
    VIEW-AS DIALOG-BOX.  
    
FORM 
    order.order-num    COLON 20
    order.cust-num     COLON 20
      HELP "Enter customer number, double-click or press HELP for choices."
    order.order-date   COLON 20
      HELP "Enter date, double-click or press HELP for assistance."
    order.promise-date COLON 20
      HELP "Enter date, double-click or press HELP for assistance."
    order.carrier      COLON 20
      HELP "Enter carrier, double-click or press HELP for choices."
    order.instructions COLON 20
    btn-Help AT ROW 2 COL 50
    btn-Save AT ROW 4 COL 50 btn-Cancel AT ROW 6 COL 50
    WITH FRAME frm-create OVERLAY ROW 5 SIDE-LABELS CENTERED
    VIEW-AS DIALOG-BOX.       
                
/************* USER INTERFACE TRIGGERS *************/    
ON CREATE OF order
    ASSIGN order.order-num = NEXT-VALUE(NEXT-ORD-NUM).
    
ON CHOOSE OF btn-Help DO:
   RUN samples/applhelp.p.
   DISPLAY order.cust-num order.order-num order.order-date
           order.promise-date order.carrier order.instructions
           WITH FRAME frm-create. 
   ENABLE btn-Save btn-Help btn-Cancel WITH FRAME frm-create.
END.

ON HELP,MOUSE-SELECT-DBLCLICK OF order.cust-num DO:
   RUN samples/applhelp.p.
   DISPLAY s_cust_num @ order.cust-num WITH FRAME frm-create.
   DISPLAY order.order-num order.order-date
           order.promise-date order.carrier order.instructions
           WITH FRAME frm-create. 
   ENABLE btn-Save btn-Help btn-Cancel WITH FRAME frm-create.
   APPLY "RETURN" TO order.cust-num IN FRAME frm-create.
END.
ON HELP,MOUSE-SELECT-DBLCLICK OF carrier DO:
   RUN samples/applhelp.p.
   DISPLAY s_carrier @ order.carrier WITH FRAME frm-create.
   DISPLAY order.cust-num order.order-num order.order-date
           order.promise-date order.instructions
           WITH FRAME frm-create. 
   ENABLE btn-Save btn-Help btn-Cancel WITH FRAME frm-create.
   APPLY "RETURN" TO order.carrier IN FRAME frm-create.
END.

ON HELP,MOUSE-SELECT-DBLCLICK OF order-date DO:
   IF INPUT FRAME frm-create order.order-date <> ? THEN
      s_date = INPUT FRAME frm-create order.order-date.
   RUN samples/applhelp.p.
   DISPLAY order.cust-num order.order-num order.order-date
           order.promise-date order.carrier order.instructions
           WITH FRAME frm-create. 
   ENABLE btn-Save btn-Help btn-Cancel WITH FRAME frm-create.
   IF s_date <> ? THEN DO:
      order.promise-date = s_date + 4.
      DISPLAY s_date @ order.order-date order.promise-date
         WITH FRAME frm-create.
      APPLY "RETURN" TO order.order-date IN FRAME frm-create.
   END.
END.
ON HELP,MOUSE-SELECT-DBLCLICK OF promise-date DO:
   IF INPUT FRAME frm-create order.promise-date <> ? THEN
      s_date = INPUT FRAME frm-create order.promise-date.
   RUN samples/applhelp.p.
   DISPLAY order.cust-num order.order-num order.order-date
           order.promise-date order.carrier order.instructions
           WITH FRAME frm-create. 
   ENABLE btn-Save btn-Help btn-Cancel WITH FRAME frm-create.
   IF s_date <> ? THEN DO:
      DISPLAY s_date @ order.promise-date WITH FRAME frm-create.
      APPLY "RETURN" TO order.promise-date IN FRAME frm-create.
   END.
END.

           
ON CHOOSE OF btn-Create DO:
  HIDE FRAME frm-order NO-PAUSE.
  DO TRANSACTION ON ENDKEY UNDO, LEAVE:
     CREATE order.
     order.promise-date = TODAY + 4.
     DISPLAY order.cust-num order.order-num order.order-date
             order.promise-date order.carrier order.instructions
             WITH FRAME frm-create. 
     UPDATE  order.cust-num
                 order.order-date order.promise-date 
                 order.carrier order.instructions
                 btn-Save btn-Cancel btn-Help
                 WITH FRAME frm-create.
     DO WITH FRAME frm-create:
        ASSIGN order.order-date order.promise-date
               order.carrier order.instructions.
     END. /* DO WITH FRAME */
   END.   /* DO TRANSACTION */
   HIDE FRAME frm-create NO-PAUSE.
   VIEW FRAME frm-order.
   DISPLAY order.cust-num order.order-num order.order-date
           order.promise-date order.carrier order.instructions
           WITH FRAME frm-order.
   ENABLE btn-Create btn-Exit WITH FRAME frm-order.
END.      /* ON CHOOSE OF btn-Create */

/*************  MAIN LOGIC ********************/ 
FIND FIRST order.
PAUSE 0.
DISPLAY order.cust-num order.order-num order.order-date
        order.promise-date order.carrier order.instructions
        WITH FRAME frm-order. 
UPDATE btn-Create btn-Exit WITH FRAME frm-order.
HIDE ALL NO-PAUSE.    








































