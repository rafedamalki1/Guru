/* r-lstevt.p */

DEFINE VARIABLE msg_watcher AS CHARACTER FORMAT "x(50)" NO-UNDO.
DEFINE VARIABLE fi AS CHARACTER FORMAT "x(50)" LABEL "Fill-in" NO-UNDO.
DEFINE VARIABLE edit AS CHARACTER VIEW-AS EDITOR SIZE 10 BY 2 NO-UNDO.

DEFINE BUTTON exit AUTO-GO LABEL "Go to Exit".
DEFINE BUTTON test LABEL "Test" SIZE 15 BY 3.
 
DEFINE VARIABLE txt0 AS CHARACTER FORMAT "x(75)" NO-UNDO.

DISPLAY "Feel free to move around..." VIEW-AS TEXT
     SKIP fi SKIP (0.1)
     edit AT 2 SKIP (0.1)
     test AT 10
     exit AT 50 SKIP (0.5)
   WITH FRAME f SIDE-LABELS.
   
DISPLAY txt0 LABEL "LAST-EVENT:LABEL FUNCTION (TYPE) > SELF:TYPE LABEL"
WITH FRAME report 4 DOWN CENTERED TITLE "Messages".

ON RETURN, TAB, ANY-PRINTABLE, GO ANYWHERE
   RUN msgwatch. /* This procedure uses LAST-EVENT. */
   
ENABLE ALL WITH FRAME f.

WAIT-FOR GO OF FRAME f FOCUS fi.

PROCEDURE msgwatch.
   txt0 = LAST-EVENT:LABEL + " " + LAST-EVENT:FUNCTION + " [" +
          LAST-EVENT:EVENT-TYPE + "]-->" + SELF:TYPE + " ".
   IF CAN-QUERY(SELF, "LABEL")
   THEN txt0 = txt0 + SELF:LABEL.
   ELSE txt0 = txt0 + STRING(SELF).
   
   DISPLAY txt0 WITH FRAME report.
   DOWN WITH FRAME report.
END PROCEDURE.
