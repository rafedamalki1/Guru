/************************************************************************************
        PROCEDURE: addweek.dem

        PURPOSE:   Demonstration program for "addweek" tool

        SYNTAX:    "RUN samples/demo/addweek.dem"

        REMARKS:   This program shows some samples of the tools
                   abilities and then allows the user to try some
                   on his/her own.

        PARAMETERS:NONE

        AUTHORS:   Progress Consulting
        DATE:      March 1993

        LAST INSPECTED:
        INSPECTED BY:

 ************************************************************************************/
 /* Copyright(c) PROGRESS SOFTWARE CORPORATION, 1993 - All Rights Reserved.            */
 
/*Code_Start*/

DEFINE VARIABLE in-sample  AS INT EXTENT 10 NO-UNDO INIT 
[190001,192034,193743,194148,194537,192343,190444,195523,195844,196544].
DEFINE VARIABLE in-add     AS INT EXTENT 10 FORMAT "->9" NO-UNDO INIT 
[34,-2,33,-36,40,-3,45,10,-10,50].
DEFINE VARIABLE loop       AS INT NO-UNDO.

DEFINE VARIABLE inweek  AS INT NO-UNDO.   /* Input week , eg 199042    */
DEFINE VARIABLE numweek AS INT NO-UNDO    /* Number of weeks to be     */  
                FORMAT "->9".                   /* added (or subtracted if   */
                                             /* numweek is negative)      */
DEFINE VARIABLE newweek AS INT FORMAT "999999" NO-UNDO.
                                         /* Output week, eg 199052    */
DEFINE VARIABLE samples AS CHAR VIEW-AS EDITOR SIZE 25 BY 8 
                                SCROLLBAR-VERTICAL NO-UNDO.
DEFINE VARIABLE wdate   AS DATE NO-UNDO.   /* Work date */

DEFINE BUTTON bQUIT LABEL "EXIT" AUTO-ENDKEY.
DEFINE BUTTON bOK   LABEL "OK"   AUTO-GO.


FORM samples NO-LABEL WITH FRAME samples_frm ROW 3 COL 2 TITLE "Samples".
FORM 
    SKIP(1)
   SPACE(3) "In Week" SPACE(3) inweek FORMAT "999999" NO-LABEL 
   HELP "Enter a date in YYYYWW format (e.g. 199042)"
   "+/- Offset" numweek NO-LABEL SKIP(1)
   SPACE(3) "New Week" SPACE(3) newweek NO-LABEL
   SKIP(1) 
   SPACE(2) bOK SPACE(2) bQUIT 
   SKIP(2)
   WITH FRAME frm-week WIDTH 40 TITLE "Try One"
   VIEW-AS DIALOG-BOX.

ASSIGN
   FRAME frm-week:ROW = 3
   FRAME frm-week:COLUMN = 35.

VIEW FRAME samples_frm.
VIEW FRAME frm-week.
ENABLE ALL WITH FRAME samples_frm.
ENABLE ALL WITH FRAME frm-week.
DISABLE newweek WITH FRAME frm-week.
      
RUN sampler.

REPEAT:
   UPDATE inweek numweek WITH FRAME frm-week.
   RUN samples/addweek.p 
          (INPUT INPUT inweek,
           INPUT INPUT numweek,
           OUTPUT newweek).
   DISPLAY newweek WITH FRAME frm-week.
   REPEAT loop = 2 TO 10:
        ASSIGN in-sample[loop - 1] = in-sample[loop]
               in-add[loop - 1]    = in-add[loop].
   END.
   samples:SCREEN-VALUE = samples:SCREEN-VALUE + STRING(INPUT inweek) + 
             (if numweek > 0 THEN "  +" ELSE "  ") + STRING(INPUT numweek) +    
             "   " + STRING(newweek) + CHR(10).
   samples:CURSOR-OFFSET = samples:LENGTH.
END.
HIDE FRAME samples_frm NO-PAUSE.
HIDE FRAME frm-week NO-PAUSE.


PROCEDURE SAMPLER.
REPEAT loop = 1 TO 10:
  RUN samples/addweek.p (INPUT in-sample[loop],
                     INPUT in-add[loop],
                     OUTPUT newweek).
  samples:SCREEN-VALUE IN FRAME samples_frm
          = samples:SCREEN-VALUE + string(in-sample[loop]) + 
          (if in-add[loop] > 0 THEN "  +" ELSE "  ") + string(in-add[loop]) + 
          "   " + string(newweek) + CHR(10).
  samples:CURSOR-OFFSET = samples:LENGTH.
END.
END PROCEDURE.
