 /*
     Filename: ByggInnePdf.P
*/
&Scoped-define NEW 
{GLOBVAR2DEL1.I}
{pdf_StartInc.i}
DEFINE TEMP-TABLE innehall NO-UNDO
{BYGGINNEHALL.I}
DEFINE INPUT PARAMETER TABLE FOR innehall.
DEFINE VARIABLE utberapph  AS HANDLE NO-UNDO.
DEFINE VARIABLE utfil      AS CHARACTER NO-UNDO.
DEFINE VARIABLE sidrakn    AS INTEGER NO-UNDO.
DEFINE VARIABLE logga      AS CHARACTER NO-UNDO.
DEFINE VARIABLE Vitems     AS INTEGER NO-UNDO.
DEFINE VARIABLE vline     AS INTEGER NO-UNDO.
DEFINE VARIABLE Vrow       AS INTEGER NO-UNDO.
DEFINE VARIABLE Vrow2       AS INTEGER NO-UNDO.
DEFINE VARIABLE Vcat-desc  AS CHARACTER EXTENT 4 FORMAT "X(40)" NO-UNDO.
DEFINE VARIABLE h       AS INTEGER NO-UNDO.
DEFINE VARIABLE w       AS INTEGER NO-UNDO.
DEFINE VARIABLE a          AS DECIMAL NO-UNDO.
DEFINE VARIABLE mtrlrubrik AS LOGICAL NO-UNDO.
DEFINE VARIABLE forsta     AS LOGICAL NO-UNDO.
DEFINE VARIABLE spann AS LOGICAL NO-UNDO.
DEFINE VARIABLE tomrad AS LOGICAL NO-UNDO.
DEFINE VARIABLE typsnyttstorlek AS DECIMAL NO-UNDO.
IF Guru.Konstanter:globforetag = "cGRAN" OR Guru.Konstanter:globforetag = "ELPA" THEN typsnyttstorlek = 12.
ELSE typsnyttstorlek = 10.
{AONRUTSID.I}
ASSIGN
mtrlrubrik = FALSE
logga = ?
forsta = FALSE
spann = FALSE
sidrakn = 1.
utfil = SESSION:TEMP-DIRECTORY.        
{SESSIONTEMPDIR.I}
IF SESSION:CLIENT-TYPE = "WEBCLIENT" THEN utfil = webclienttempdir.
IF Guru.GlobalaVariabler:plusaonr = "" OR Guru.GlobalaVariabler:plusaonr = ? THEN DO:
   utfil = utfil + "ByggInneh" + STRING(TIME) + ".pdf".
END.
ELSE DO:
   utfil = utfil + "ByggInneh" + TRIM (Guru.GlobalaVariabler:plusaonr) + TRIM(STRING(Guru.GlobalaVariabler:plusdnr,Guru.Konstanter:varforetypchar[1])) + STRING(TIME) + ".pdf".
END.
 OS-DELETE VALUE(utfil).
/*FOREBILD*/
{BILDPDF.I}
IF Guru.GlobalaVariabler:globsids = 0 THEN Guru.GlobalaVariabler:globsids = 73.
DEFINE VARIABLE pdfsida AS INTEGER NO-UNDO.
IF logga NE ? THEN pdfsida = Guru.GlobalaVariabler:globsids - 3.
ELSE pdfsida = Guru.GlobalaVariabler:globsids.
RUN startpdf_UI.
RUN pdf_close IN h_PDFinc ("Spdf").
IF VALID-HANDLE(h_PDFinc) THEN DELETE PROCEDURE h_PDFinc NO-ERROR. 
h_PDFinc = ?.
RUN OPENDOC.P (utfil,"","Acrobat.exe",NO).

PROCEDURE startpdf_UI:
   DEFINE VARIABLE extravar AS CHARACTER NO-UNDO.   
   DEFINE VARIABLE radut AS CHARACTER NO-UNDO.
   RUN pdf_new IN h_PDFinc ("Spdf",utfil).
   RUN pdf_set_LeftMargin IN h_PDFinc ("Spdf",40).
   /* Dokument Information */ 
   RUN pdf_set_info("Spdf","Author","Elpool i Umeå AB").
   RUN pdf_set_info("Spdf","Subject","Innehållsförteckning").
   RUN pdf_set_info("Spdf","Title","Byggprotokoll").
   RUN pdf_set_info("Spdf","Keywords","Beredning, Innehållsförteckning").
   RUN pdf_set_info("Spdf","Creator","PDFinclude").
   RUN pdf_set_info("Spdf","Producer","ByggInnePdf.P").
   /*Skapa ny sida*/
  
   RUN new_page.
   RUN pdf_set_font IN h_PDFinc ("Spdf","Courier",9).
   tomrad = TRUE.
   FOR EACH innehall WHERE innehall.SIDA = 9999999 NO-LOCK:
      RUN pdf_text IN h_PDFinc ("Spdf", innehall.UT).
      RUN pdf_skip IN h_PDFinc ("Spdf").
      DELETE innehall.
   END.
   radut = "".
   SUBSTRING(radut,1,10) = "Byggnr".
   SUBSTRING(radut,12,6) = "Fri Id".
   SUBSTRING(radut,43,7) = Guru.Konstanter:varforetypchar[13].
   SUBSTRING(radut,64,12) = "Konstruktion".
   SUBSTRING(radut,97,3) = "Sid".
   RUN pdf_text IN h_PDFinc ("Spdf", radut).
   RUN pdf_skip IN h_PDFinc ("Spdf").   
   FOR EACH innehall NO-LOCK:
      vline = vline + 1.      
      IF vline >= pdfsida THEN DO:         
         RUN new_page.
         radut = "".
         SUBSTRING(radut,1,10) = "Byggnr".
         SUBSTRING(radut,12,6) = "Fri Id".
         SUBSTRING(radut,43,7) = Guru.Konstanter:varforetypchar[13].
         SUBSTRING(radut,64,12) = "Konstruktion".
         SUBSTRING(radut,97,3) = "Sid".
         RUN pdf_text IN h_PDFinc ("Spdf", radut).
         RUN pdf_skip IN h_PDFinc ("Spdf").
      END.
      radut = "".                                                                              
      IF innehall.BYGGNR NE "" THEN DO:
         IF INTEGER(innehall.BYGGNR) NE ? THEN SUBSTRING(radut,1,10) = STRING(INTEGER(innehall.BYGGNR),">>>>>>>>>9").
      END.   
      SUBSTRING(radut,12,30) = SUBSTRING(innehall.FRIID,1,30).
      SUBSTRING(radut,43,20) =  SUBSTRING(innehall.KARTREF,1,20).
      SUBSTRING(radut,64,22) = TRIM(innehall.F1).       
      SUBSTRING(radut,97) = STRING(innehall.SIDA,">>>9").     
      RUN pdf_text IN h_PDFinc ("Spdf", radut).
      RUN pdf_skip IN h_PDFinc ("Spdf").
   END.    
END.

PROCEDURE new_page.
   RUN pdf_new_page IN h_PDFinc ("Spdf").
   RUN pdf_set_LeftMargin IN h_PDFinc ("Spdf",40).
   RUN pdf_set_TopMargin IN h_PDFinc ("Spdf",40).
   /*Sätt font*/
   RUN pdf_set_font IN h_PDFinc ("Spdf","Courier",9).
   RUN pdf_text_color IN h_PDFinc ("Spdf",0.0,0.0,0.0).
   ASSIGN 
   vline = 0 
   tomrad  = true
   Vrow = pdf_PageHeight("Spdf") - pdf_TopMargin("Spdf"). 
   RUN pdf_text_align IN h_PDFinc ("Spdf",aoutvar + " Sid " + STRING(sidrakn),"CENTER",pdf_PageWidth("Spdf") - 90,pdf_PageHeight("Spdf") - 15).
   RUN pdf_skip IN h_PDFinc ("Spdf").
   /*Init. logga*/
   IF forsta = FALSE THEN DO:
      IF logga NE ? THEN DO:
         RUN pdf_load_image IN h_PDFinc ("Spdf","ProSysLogo",logga).
         h = pdf_ImageDim ("Spdf","ProSysLogo","HEIGHT").
         w = pdf_ImageDim ("Spdf","ProSysLogo","WIDTH").
         IF h > 30 THEN DO:
            a = 30 / h.
            h = a * h.
            w = a * w.            
         END.
         RUN pdf_place_image IN h_PDFinc ("Spdf","ProSysLogo",pdf_LeftMargin("Spdf"), pdf_TopMargin("Spdf"),w,h).
         RUN pdf_skipn IN h_PDFinc ("Spdf",3).
         vline = 3.
      END.
      forsta = TRUE.
   END.
   ELSE DO:
      /*Placera ut logga överst på varje sida*/
      IF logga NE ? THEN DO:
         RUN pdf_place_image IN h_PDFinc ("Spdf","ProSysLogo",pdf_LeftMargin("Spdf"), pdf_TopMargin("Spdf"),w,h).
         RUN pdf_skipn IN h_PDFinc ("Spdf",3).
         vline = 3.
      END.
   END.
   sidrakn = sidrakn + 1.
END PROCEDURE.


