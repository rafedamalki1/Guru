/* 
rblist1.p 

Running Report Engine from an application - TABLE INTERFACE

Generating a list of reports with _getname.p, displaying all the
reports, then displaying the RB-STATUS field.
            
This procedure assumes that all of the reports in the list generated by 
_getname.p use the SPORTS database.  Report Engine connects to the database 
only once (on the command-line to prore.exe) and the same connection is used
for each report since the '=>' syntax is used in the RB-DB-CONNECTION field
of the RBREPORT table.  This procedure assumes that the sports database            
is located in c:\data.

You must be connected to the Runtable database in multi-user mode before 
running this procedure.

This procedure takes a report library name as its input argument, for
example:

RUN rblist1.p("c:\dlc\src\aderb\rbsample.prl").

*/

DEF VAR  report-list  AS CHARACTER INITIAL "".
DEF VAR  report-count  AS INTEGER INITIAL 0.
DEF VAR  report-number AS INTEGER INITIAL  0.
DEF VAR  current-report-name AS CHARACTER INITIAL "".

/* Define input parameter to procedure */

DEF INPUT PARAMETER report-library AS CHARACTER.

/* Run _getname.p to generate a list of reports in the specified library */

RUN aderb/_getname.p(report-library, OUTPUT report-list, 
         OUTPUT report-count).

/* Add the reports to the Runtable database */

REPEAT report-number = 1 TO report-count:

  CREATE rbreport.
    
  current-report-name = ENTRY(report-number, report-list).
   
  ASSIGN 
    RBREPORT.RB-REPORT-LIBRARY = report-library
    RBREPORT.RB-REPORT-NAME = current-report-name
    RBREPORT.RB-DB-CONNECTION = "sports => logsport"
    RBREPORT.RB-PRINT-DESTINATION = "D"
    RBREPORT.RB-DISPLAY-STATUS = yes
    RBREPORT.RB-DISPLAY-ERRORS = yes. 
     
  RELEASE RBREPORT.  
              
END.

/* Run each report in RBREPORT table and update the RB-STATUS field */
    
RUN aderb\_prore(false, "-db Runtable -S servername -H hostname -N networktype
                         -db c:\data\sports -1 -ld logsport -rbupds").

/* For each record, view RB-STATUS field as Alert Box and delete record. */
 
 FOR EACH RBREPORT:
    
      MESSAGE "Status of Report '" + RBREPORT.RB-REPORT-NAME + 
               "': " + RBREPORT.RB-STATUS 
         VIEW-AS ALERT-BOX.
        
      DELETE RBREPORT.
 END.
   
 
